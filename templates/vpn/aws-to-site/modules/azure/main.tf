## AWS side resources/services
# AWS Customer Gateway (require Azure VPN Gateway info)
resource "aws_customer_gateway" "azure_gw" {
  count = 2

  tags = {
    Name = "${var.name_prefix}-azure-side-gw-${count.index + 1}"
  }
  bgp_asn    = var.bgp_asn
  ip_address = azurerm_public_ip.pub_ip[count.index].ip_address
  type       = "ipsec.1"
}

# AWS VPN Connection with custom inside CIDR blocks (due to Azure VPN Gateway's requirement)
resource "aws_vpn_connection" "to_azure" {
  count = 2

  tags = {
    Name = "${var.name_prefix}-to-azure-${count.index + 1}"
  }
  vpn_gateway_id      = var.aws_vpn_gateway_id
  customer_gateway_id = aws_customer_gateway.azure_gw[count.index].id
  type                = "ipsec.1"

  ## Set custom CIDR blocks for inside tunnel addresses (Azure VPN Gateway's requirement)
  # When setting AWS inside IPv4 CIDR blocks to 169.254.21.0/30, 
  # AWS will use 169.254.21.1 and
  # Azure will use 169.254.21.2.
  tunnel1_inside_cidr = count.index == 0 ? "169.254.21.0/30" : "169.254.22.0/30"
  tunnel2_inside_cidr = count.index == 0 ? "169.254.21.4/30" : "169.254.22.4/30"
}


## Azure side resources/services
# Azure Virtual Network
data "azurerm_virtual_network" "existing" {

  name                = var.virtual_network_name
  resource_group_name = var.resource_group_name
}

# Gateway Subnet (Azure requirement: "GatewaySubnet" is required for VPN Gateway)
resource "azurerm_subnet" "gateway" {

  name                 = "GatewaySubnet"
  resource_group_name  = var.resource_group_name
  virtual_network_name = data.azurerm_virtual_network.existing.name
  address_prefixes     = [var.gateway_subnet_cidr]
}

# Public IPs for Azure VPN Gateway
resource "azurerm_public_ip" "pub_ip" {
  count = 2 # 2 Public IPs for Active-Active configuration

  name                = "${var.name_prefix}-vpn-ip-${count.index + 1}"
  location            = var.region
  resource_group_name = var.resource_group_name
  allocation_method   = "Static"
  sku                 = "Standard"
  zones               = ["1", "2", "3"] # Availability zones
}

# Azure VPN Gateway with pre-allocated APIPA addresses
resource "azurerm_virtual_network_gateway" "vpn_gw" {

  name                = "${var.name_prefix}-vpn-gateway"
  location            = var.region
  resource_group_name = var.resource_group_name

  type     = "Vpn"
  vpn_type = "RouteBased"

  active_active = true
  enable_bgp    = true
  sku           = var.vpn_sku

  ip_configuration {
    name                          = "${var.name_prefix}-gateway-config-1"
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.pub_ip[0].id
    subnet_id                     = azurerm_subnet.gateway.id
  }

  ip_configuration {
    name                          = "${var.name_prefix}-gateway-config-2"
    private_ip_address_allocation = "Dynamic"
    public_ip_address_id          = azurerm_public_ip.pub_ip[1].id
    subnet_id                     = azurerm_subnet.gateway.id
  }

  # https://learn.microsoft.com/en-us/azure/vpn-gateway/bgp-howto
  # The Azure APIPA BGP IP address field is optional. 
  # If your on-premises VPN devices use APIPA address for BGP, 
  # you must select an address from the Azure-reserved APIPA address range for VPN, 
  # which is from 169.254.21.0 to 169.254.22.255.
  bgp_settings {
    asn = var.bgp_asn
    peering_addresses {
      ip_configuration_name = "${var.name_prefix}-gateway-config-1"
      apipa_addresses = [
        # note - AWS-sides BGP address cannot be used due to Azure's requirments.
        # aws_vpn_connection.to_azure[0].tunnel1_cgw_inside_address,
        # aws_vpn_connection.to_azure[0].tunnel2_cgw_inside_address
        "169.254.21.2", # For tunnel1 of first connection
        "169.254.22.2"  # For tunnel2 of first connection
      ]
    }
    peering_addresses {
      ip_configuration_name = "${var.name_prefix}-gateway-config-2"
      apipa_addresses = [
        # Note - Inside tunnel addresses generated by AWS may not be used 
        # due to Azure's APIPA address range (from 169.254.21.0 to 169.254.22.255).
        # aws_vpn_connection.to_azure[1].tunnel1_cgw_inside_address,
        # aws_vpn_connection.to_azure[1].tunnel2_cgw_inside_address
        "169.254.21.6", # For tunnel1 of second connection
        "169.254.22.6"  # For tunnel2 of second connection
      ]
    }
  }
}

# Azure Local Network Gateway (require AWS VPN gateway info)
resource "azurerm_local_network_gateway" "aws_gw" {
  count = 4

  name                = "${var.name_prefix}-aws-side-${count.index + 1}"
  location            = var.region
  resource_group_name = var.resource_group_name

  gateway_address = count.index % 2 == 0 ? aws_vpn_connection.to_azure[floor(count.index / 2)].tunnel1_address : aws_vpn_connection.to_azure[floor(count.index / 2)].tunnel2_address

  bgp_settings {
    asn                 = count.index % 2 == 0 ? aws_vpn_connection.to_azure[floor(count.index / 2)].tunnel1_bgp_asn : aws_vpn_connection.to_azure[floor(count.index / 2)].tunnel2_bgp_asn
    bgp_peering_address = count.index % 2 == 0 ? aws_vpn_connection.to_azure[floor(count.index / 2)].tunnel1_vgw_inside_address : aws_vpn_connection.to_azure[floor(count.index / 2)].tunnel2_vgw_inside_address
  }
}

# Azure VPN Connection 
resource "azurerm_virtual_network_gateway_connection" "to_aws" {
  count = 4

  name                = "${var.name_prefix}-to-aws-${count.index + 1}"
  location            = var.region
  resource_group_name = var.resource_group_name

  type                       = "IPsec"
  virtual_network_gateway_id = azurerm_virtual_network_gateway.vpn_gw.id
  local_network_gateway_id   = azurerm_local_network_gateway.aws_gw[count.index].id
  shared_key                 = count.index % 2 == 0 ? aws_vpn_connection.to_azure[floor(count.index / 2)].tunnel1_preshared_key : aws_vpn_connection.to_azure[floor(count.index / 2)].tunnel2_preshared_key

  enable_bgp = true
}
